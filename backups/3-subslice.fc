{-
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref.
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...

-}

(int) tlen(tuple t) asm "TLEN";
(int) brembits(builder b) asm "BREMBITS";
(slice) subslice(slice s, int l, int _, int r, int _) asm "SUBSLICE";
(int) is_prefix(slice s1, slice s2) asm "SDPFX";
(int) ubitsize(int n) asm "UBITSIZE";
(int) sign(int n) asm "SGN";

() recv_internal() {
}

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) method_id {
    (int flen, int vlen) = (ubitsize(flag), ubitsize(value));
    slice fslice = begin_cell().store_uint(flag, flen).end_cell().begin_parse();

    builder b = begin_cell();
    tuple lst = null();

    int prev_wlen = 0;
    slice prev_wslice = begin_cell().end_cell().begin_parse();

    ;; 340480 -> 339877 -> 339867
    ;; 421671 / 412863

    while (~ null?(linked_list)) {
        slice s = linked_list.begin_parse();

        int wlen = min(slice_bits(s), 1023 - prev_wlen - vlen);
        slice wslice = begin_cell().store_slice(prev_wslice).store_slice(s~load_bits(wlen)).end_cell().begin_parse();
        wlen += prev_wlen;

        int i = 0;
        int prev = 0;
        while (i <= wlen - flen) {
            slice sub = subslice(wslice, i, 0, flen, 0);

            if (is_prefix(fslice, sub)) {
                slice data = subslice(wslice, prev, 0, i - prev, 0);
                int required = i - prev + vlen;

                if (brembits(b) < required) {
                    lst = cons(b, lst);
                    b = begin_cell().store_slice(data).store_uint(value, vlen);
                } else {
                    b = b.store_slice(data).store_uint(value, vlen);
                }

                i += flen;
                prev = i;
            } else {
                i += 1;
            }
        }

        int rem = wlen - prev;

        if (rem < flen) {
            prev_wlen = rem;
            prev_wslice = wslice.slice_last(rem);
        } else {
            slice data = subslice(wslice, prev, 0, rem - flen + 1, 0);

            if (brembits(b) < rem) {
                lst = cons(b, lst);
                b = begin_cell().store_slice(data);
            } else {
                b = b.store_slice(data);
            }

            prev_wlen = flen - 1;
            prev_wslice = wslice.slice_last(flen - 1);
        }

        if (~ slice_refs_empty?(s)) {
            linked_list = s.preload_ref();
        } else {
            linked_list = null();
        }
    }

    if (prev_wlen > 0) {
        lst = cons(b, lst);
        b = begin_cell().store_slice(prev_wslice);
    }

    while (~ null?(lst)) {
        builder curr = lst~list_next();
        b = curr.store_ref(b.end_cell());
    }

    return b.end_cell();
}
