{-
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref.
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Flag and the value may be of different lengths.
  When there are overlapping flags, only the first one
  from the overlap needs to be replaced (for example, if the flag is 101,
  value is 111, and string is 10101, then the result would be 11101, but if
  the string is 1010101, then the result would be 1110111).
  Every cell in the input linked list, except for the last one
  (the one that is the deepest in the tree), is guaranteed to be full
  (guaranteed to contain 1023 bits).
  The requirements on the output cell are more loose - only the concatenation of bits
  from the linked list needs to match that of the expected answer, but the structure
  itself may be different (for example, for the purposes of comparing the answer,
  a cell with bit 1 and a ref containing bits 10 is the same as a cell containing
  bit 11 and a ref with bit 0 - they both make up a bitstring 110).
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...

-}

(slice, int) check_prefix(slice s1, slice s2) asm "SDBEGINSXQ";
(int) ubitsize(int n) asm "UBITSIZE";

() recv_internal() {
}

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) method_id {
    (int flen, int vlen) = (ubitsize(flag), ubitsize(value));
    slice fslice = begin_cell().store_uint(flag, flen).end_cell().begin_parse();

    builder b = begin_cell();
    tuple lst = null();

    int prev_wlen = 0;
    slice prev_wslice = null();

    ;; 320189 / 305020 b300

    while (~ null?(linked_list)) {
        slice s = linked_list.begin_parse();
        int sz = slice_bits(s);

        while (sz > 0) {
            int wlen = min(sz, 511 - prev_wlen);
            sz -= wlen;

            builder wbuilder = null();
            slice word = s~load_bits(wlen);

            if (prev_wlen > 0) {
                wbuilder = begin_cell().store_slice(prev_wslice).store_slice(word);
                wlen += prev_wlen;
            } else {
                wbuilder = begin_cell().store_slice(word);
            }

            slice wslice = wbuilder.end_cell().begin_parse();
            int shift = wlen - flen;

            lst = cons(b, lst);
            b = begin_cell();

            while (shift >= 0) {
                (wslice, int status) = check_prefix(wslice, fslice);
                if (status) {
                    b~store_uint(value, vlen);
                    shift -= flen;
                } else {
                    int bit = wslice~load_uint(1);
                    b~store_uint(bit, 1);
                    shift -= 1;
                }
            }

            prev_wlen = min(wlen, flen + shift);
            prev_wslice = wslice;
        }

        if (~ slice_refs_empty?(s)) {
            linked_list = s.preload_ref();
        } else {
            linked_list = null();
        }
    }

    if (prev_wlen > 0) {
        lst = cons(b, lst);
        b = begin_cell().store_slice(prev_wslice);
    }

    while (~ null?(lst)) {
        builder curr = lst~list_next();
        b = curr.store_ref(b.end_cell());
    }

    return b.end_cell();
}