{-
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref.
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Flag and the value may be of different lengths.
  When there are overlapping flags, only the first one
  from the overlap needs to be replaced (for example, if the flag is 101,
  value is 111, and string is 10101, then the result would be 11101, but if
  the string is 1010101, then the result would be 1110111).
  Every cell in the input linked list, except for the last one
  (the one that is the deepest in the tree), is guaranteed to be full
  (guaranteed to contain 1023 bits).
  The requirements on the output cell are more loose - only the concatenation of bits
  from the linked list needs to match that of the expected answer, but the structure
  itself may be different (for example, for the purposes of comparing the answer,
  a cell with bit 1 and a ref containing bits 10 is the same as a cell containing
  bit 11 and a ref with bit 0 - they both make up a bitstring 110).
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...

-}

(slice, slice) sload_ref(slice s) asm "LDREFRTOS";
(slice, int) check_prefix(slice s1, slice s2) asm "SDBEGINSXQ";
(int) ubitsize(int n) asm "UBITSIZE";

() recv_internal() {
}

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) method_id {
    (int flen, int vlen) = (ubitsize(flag), ubitsize(value));
    slice fslice = begin_cell().store_uint(flag, flen).end_cell().begin_parse();

    builder b = begin_cell();
    tuple lst = null();

    ;; 408694

    slice s = linked_list.begin_parse();
    int sz = slice_bits(s);

    while (sz > 0) {
        slice ns = null();
        int nsz = 0;

        if (~ slice_refs_empty?(s)) {
            (_, ns) = s.sload_ref();
            nsz = slice_bits(ns);
        }

        int i = 0;
        int status = 0;
        int len = sz - flen;

        while (i <= len) {
            lst = cons(b, lst);
            b = begin_cell();

            int j = i;
            int jlen = min(i + 510, len);

            while (j <= jlen) {
                (s, status) = check_prefix(s, fslice);
                if (status) {
                    b~store_uint(value, vlen);
                    j += flen;
                } else {
                    int bit = s~load_uint(1);
                    b~store_uint(bit, 1);
                    j += 1;
                }
            }

            i = j;
        }

        if (nsz > 0) {
            ;; sz - i < flen
            ;; available > 0

            ;; todo: get rid of extra cell creation
            int max = min(nsz, flen - 1);
            slice it = begin_cell().store_slice(s).store_slice(ns.preload_bits(max)).end_cell().begin_parse();

            while (i < sz) {
                (it, status) = check_prefix(it, fslice);
                if (status) {
                    b~store_uint(value, vlen);
                    i += flen;
                } else {
                    int bit = it~load_uint(1);
                    b~store_uint(bit, 1);
                    i += 1;
                }
            }

            if (i > sz) {
                ns~skip_bits(i - sz);
                nsz -= i - sz;
            }
        } else {
            b = b.store_slice(s);
        }

        s = ns;
        sz = nsz;
    }

    while (~ null?(lst)) {
        builder curr = lst~list_next();
        b = curr.store_ref(b.end_cell());
    }

    return b.end_cell();
}